plugins {
    id 'java'
    id 'antlr'
    id 'io.micronaut.application' version '3.7.10'
    id 'io.micronaut.minimal.application' version '3.7.10'
    id 'io.micronaut.docker' version '3.7.10'
    id 'com.github.node-gradle.node' version '3.3.0'
    id 'com.vaadin' version '23.3.10'
}

compileJava {
    targetCompatibility = '17'
    sourceCompatibility = '17'
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://maven.vaadin.com/vaadin-addons" }
    }

    dependencies {
        constraints {
            implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.7'
            implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.8'

            implementation group: 'io.micronaut', name: 'micronaut-http-server-netty', version: '3.10.1'
            implementation group: 'io.micronaut', name: 'micronaut-http-client', version: '3.10.1'
            implementation group: 'io.micronaut.security', name: 'micronaut-security-oauth2', version: '3.10.1'
            implementation group: 'io.micronaut.security', name: 'micronaut-security-jwt', version: '3.10.1'
            implementation group: 'io.micronaut.flyway', name: 'micronaut-flyway', version: '5.5.0'
            implementation group: 'io.micronaut.sql', name: 'micronaut-jdbc-hikari', version: '4.8.1'
            implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '6.1.0.202203080745-r'
            implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
            implementation group: 'com.google.guava', name: 'guava', version: '32.1.1-jre'
            implementation group: 'com.github.lonely-lockley', name: 'ltsv-parser', version: '1.1.0'
            implementation group: 'com.auth0', name: 'java-jwt', version: '4.4.0'
            implementation group: 'com.auth0', name: 'jwks-rsa', version: '0.22.1'
            implementation group: 'org.mybatis', name: 'mybatis', version: '3.5.13'
            implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'

            implementation group: 'com.vaadin', name: 'vaadin-core', version: '23.3.10'
            implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '10.0.15'
            implementation group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '10.0.15'
            implementation group: 'org.eclipse.jetty', name: 'jetty-plus', version: '10.0.15'
            implementation group: 'org.eclipse.jetty', name: 'jetty-annotations', version: '10.0.15'
            implementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '10.0.15'
            implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-jetty-server', version: '10.0.15'
            implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-jetty-api', version: '10.0.15'
            implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-servlet', version: '10.0.15'
            implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'


            testImplementation group: 'org.testng', name: 'testng', version: '6.14.3'
        }
    }

    clean.doFirst {
        delete 'out'
    }
}

def checkBuildMode() {
    return System.getenv("DEV_MODE") != "true"
}

test {
    useJUnitPlatform()
}

subprojects {
    task printDependencies(type: DependencyReportTask) {}
}

project('common') {
    dependencies {
        implementation group: 'org.slf4j', name: 'slf4j-api'
        implementation group: 'ch.qos.logback', name: 'logback-classic'
    }
}

project('insight-lang') {
    apply plugin: 'antlr'

    dependencies {
        antlr group: 'org.antlr', name: 'antlr4', version: '4.10.1'
        testImplementation group: 'org.testng', name: 'testng'
    }

    generateGrammarSource {
        outputDirectory = new File("${project.buildDir}/$GEN_JAVA_SRC/$GEN_JAVA_PKG".toString())
    }

    sourceSets.main.java.srcDirs += ["${project.buildDir}/$GEN_JAVA_SRC"]
}

project('translator') {
    apply plugin: 'io.micronaut.application'
    apply plugin: 'io.micronaut.minimal.application'
    apply plugin: 'io.micronaut.docker'

    dependencies {
        implementation project(':common')
        implementation project(':insight-lang')

        implementation group: 'io.micronaut', name: 'micronaut-http-server-netty'
        implementation group: 'com.github.lonely-lockley', name: 'ltsv-parser'
        implementation group: 'org.apache.commons', name: 'commons-text'
    }

    tasks.named("dockerBuild") {
        images = ["lonelylockley/archinsight:$project.name-$project.version"]
    }

    tasks.named("dockerfile") {
        entryPoint("java", "-Xms128M", "-Xmx128M", "-cp", "/home/app/application.jar", 'com.github.lonelylockley.archinsight.TranslatorService')
    }
}

project('renderer') {
    apply plugin: 'io.micronaut.application'
    apply plugin: 'io.micronaut.minimal.application'
    apply plugin: 'io.micronaut.docker'

    dependencies {
        implementation project(':common')

        implementation group: 'io.micronaut', name: 'micronaut-http-server-netty'
    }

    tasks.named("dockerBuild") {
        images = ["lonelylockley/archinsight:$project.name-$project.version"]
    }

    tasks.named("dockerfile") {
        // tried to fix "Assertion failed: np->cells[1] (maze.c: chkSgraph: 331)" error with version update, no luck for 4.0.0
        // https://gitlab.com/graphviz/graphviz/-/issues/1408
        baseImage('alpine:3.18')
        runCommand('apk add --update --no-cache openjdk17 graphviz ttf-freefont')
        entryPoint("java", "-Xms128M", "-Xmx128M", "-cp", "/home/app/application.jar", 'com.github.lonelylockley.archinsight.RenderService')
    }
}

project('repository') {
    apply plugin: 'io.micronaut.application'
    apply plugin: 'io.micronaut.minimal.application'
    apply plugin: 'io.micronaut.docker'

    dependencies {
        implementation project(':common')

        implementation group: 'io.micronaut', name: 'micronaut-http-server-netty'
        implementation group: 'io.micronaut', name: 'micronaut-http-client'
        implementation group: 'io.micronaut.flyway', name: 'micronaut-flyway'
        implementation group: 'io.micronaut.sql', name: 'micronaut-jdbc-hikari'
        implementation group: 'org.mybatis', name: 'mybatis'
        implementation group: 'org.postgresql', name: 'postgresql'
    }

    tasks.named("dockerBuild") {
        images = ["lonelylockley/archinsight:$project.name-$project.version"]
    }

    tasks.named("dockerfile") {
        runCommand('apk add --update --no-cache graphviz ttf-freefont')
        entryPoint("java", "-Xms128M", "-Xmx128M", "-cp", "/home/app/application.jar", 'com.github.lonelylockley.archinsight.RepositoryService')
    }
}

project('editor-ui') {
    apply plugin: 'com.github.node-gradle.node'
    apply plugin: 'io.micronaut.application'
    apply plugin: 'io.micronaut.docker'
    apply plugin: 'com.vaadin'

    dependencies {
        implementation project(':common')

        implementation group: 'com.vaadin', name: 'vaadin-core'
        implementation group: 'com.vaadin.external.atmosphere', name: 'atmosphere-runtime'
        implementation group: 'org.eclipse.jetty', name: 'jetty-server'
        implementation group: 'org.eclipse.jetty', name: 'jetty-webapp'
        implementation group: 'org.eclipse.jetty', name: 'jetty-plus'
        implementation group: 'org.eclipse.jetty', name: 'jetty-annotations'
        implementation group: 'org.eclipse.jetty', name: 'jetty-util'
        implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-jetty-server'
        implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-jetty-api'
        implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api'
        implementation group: 'io.micronaut', name: 'micronaut-http-client'
        implementation group: 'com.google.guava', name: 'guava'
        implementation group: 'com.auth0', name: 'java-jwt'
        implementation group: 'com.auth0', name: 'jwks-rsa'
    }

    clean.doFirst {
        delete "frontend/generated/insight-lang"
    }

    task antlr(type: NpmTask) {
        dependsOn(npmInstall)
        args = ['run', 'antlr']

        doLast {
            def substs = new Properties()
            file("${project.rootDir}/insight-lang/src/main/antlr/typescript_substitutions.properties").withInputStream { substs.load(it) }
            FileTree antlrFiles = fileTree("frontend/generated/insight-lang") {
                include "**/*.ts"
            }
            antlrFiles.each { File antlrSrc ->
                println "Removing java package from $antlrSrc"
                String content = antlrSrc.getText()
                content = content.replaceAll(/(\/\* <package>)(.|\n)+?(<\/package> \*\/)/, substs.getProperty("package"))
                content = content.replaceAll(/(\/\* <import>)(.|\n)+?(<\/import> \*\/)/, substs.getProperty("import"))
                content = content.replaceAll(/(\/\* <override>)(.|\n)+?(<\/override> \*\/)/, substs.getProperty("override"))
                content = content.replaceAll(/(\/\* <helper>)(.|\n)+?(<\/helper> \*\/)/, substs.getProperty("helper"))
                content = content.replaceAll(/(\/\* <position>)(.|\n)+?(<\/position> \*\/)/, substs.getProperty("position"))
                antlrSrc.setText(content)
            }
        }
    }

    tasks.named("vaadinBuildFrontend") {
        dependsOn(antlr)
    }

    tasks.named("dockerBuild") {
        images = ["lonelylockley/archinsight:$project.name-$project.version"]
    }

    vaadin {
        pnpmEnable = false
        productionMode = checkBuildMode()
        generateBundle(checkBuildMode())
        optimizeBundle(checkBuildMode())
        vaadinBuildFrontend
    }

    tasks.named("dockerfile") {
        copyFile('layers/editor-ui', '/home/app/editor-ui')
        runCommand('apk add --update --no-cache graphviz ttf-freefont')
        entryPoint("java", "-Xms128M", "-Xmx128M", "-cp", "/home/app/application.jar:libs/*", 'com.github.lonelylockley.archinsight.Launcher')
    }

    dockerfile.doLast {
        copy {
            from "${buildDir}/docker/main/layers/resources/META-INF/VAADIN/webapp"
            into "${buildDir}/docker/main/layers/editor-ui/frontend/"
        }
        copy {
            from "frontend/static"
            into "${buildDir}/docker/main/layers/editor-ui/frontend/static"
        }
        copy {
            from "frontend/themes"
            into "${buildDir}/docker/main/layers/editor-ui/frontend/themes"
        }
        copy {
            from "frontend"
            include "*.ico"
            into "${buildDir}/docker/main/layers/editor-ui/frontend/"
        }
    }

}

project('identity') {
    apply plugin: 'io.micronaut.application'
    apply plugin: 'io.micronaut.minimal.application'
    apply plugin: 'io.micronaut.docker'

    dependencies {
        implementation project(':common')

        implementation group: 'io.micronaut', name: 'micronaut-http-server-netty'
        implementation group: 'io.micronaut', name: 'micronaut-http-client'
        implementation group: 'io.micronaut.security', name: 'micronaut-security-oauth2'
        implementation group: 'io.micronaut.security', name: 'micronaut-security-jwt'
        implementation group: 'io.micronaut.flyway', name: 'micronaut-flyway'
        implementation group: 'io.micronaut.sql', name: 'micronaut-jdbc-hikari'
        implementation group: 'com.auth0', name: 'java-jwt'
        implementation group: 'org.mybatis', name: 'mybatis'
        implementation group: 'org.postgresql', name: 'postgresql'
    }

    tasks.named("dockerBuild") {
        images = ["lonelylockley/archinsight:$project.name-$project.version"]
    }

    tasks.named("dockerfile") {
        entryPoint("java", "-Xms128M", "-Xmx128M", "-cp", "/home/app/application.jar", 'com.github.lonelylockley.archinsight.IdentityService')
    }
}
